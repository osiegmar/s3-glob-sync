plugins {
    id 'java'
    id 'com.github.spotbugs' version '4.6.0'
    id 'org.beryx.jlink' version '2.23.6'
}

group = 'de.siegmar'
version = '0.0.1'

repositories {
    mavenCentral()
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(11)
    }
    modularity.inferModulePath = true
}

test {
    useJUnitPlatform()
}

spotbugs {
    toolVersion = '4.2.1'
}

dependencies {
    implementation 'info.picocli:picocli:4.6.1'
    implementation 'software.amazon.awssdk:s3:2.16.34'
    implementation 'ch.qos.logback:logback-classic:1.2.3'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.0'
    testImplementation 'org.junit.jupiter:junit-jupiter-params:5.7.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.7.0'
}

jlink {
    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']
    mainClass = 'de.siegmar.s3globsync.S3'
    launcher {
        name = 's3-glob-sync'
    }
    mergedModule {
        requires 'java.naming';
        requires 'java.xml';
        requires 'com.fasterxml.jackson.core';
        requires 'com.fasterxml.jackson.annotation';
        requires 'com.fasterxml.jackson.databind';
        uses 'software.amazon.awssdk.http.SdkHttpService';
        provides 'software.amazon.awssdk.http.SdkHttpService' with 'software.amazon.awssdk.http.apache.ApacheSdkHttpService';
    }
}

allprojects {
    tasks.register('downloadDependencies', Copy) { Copy copy ->
        copy.into project.layout.buildDirectory.dir('dependencies')
        copy.from {
            configurations.matching { Configuration c ->
                c.isCanBeResolved()
            }.collect { Configuration c ->
                c.resolve()
            }.flatten().unique()
        }
    }
}
